# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: Oleg Rakhmanov <oleg@archlinuxarm.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Vlad Frolov <frolvlad@gmail.com>
# Contributor: Martin Rejda <rejdi@github.ksp.sk>

buildarch=4

_prefix=/usr

pkgbase=kodi-odroid
pkgname=('kodi-odroid' 'kodi-odroid-eventclients')
_commit=Krypton
pkgver=17.6
pkgrel=1
arch=('armv7h')
url="http://kodi.tv"
license=('GPL2')
makedepends=(
  'afpfs-ng' 'bluez-libs' 'boost' 'cmake' 'curl' 'doxygen' 'git' 'glew'
  'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libaacs' 'libass'
  'libbluray' 'libcdio' 'odroid-libgl' 'libmariadbclient' 'libmicrohttpd'
  'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libssh' 'libva'
  'libxrandr' 'libxslt' 'lzo' 'nasm' 'nss-mdns' 'python2-pillow'
  'python2-pybluez' 'python2-simplejson' 'rtmpdump' 'sdl2' 'sdl_image'
  'smbclient' 'swig' 'taglib' 'tinyxml' 'unzip' 'upower' 'yajl' 'zip'
  'mesa' 'libdcadec.so' 'libcec' 'cwiid'
)

source=(
	"https://github.com/Owersun/xbmc/archive/${_commit}.tar.gz"
	"001-add_mfc_decoding_toggle.patch"
	"002-dont_try_to_decode_10bit_using_mfc.patch"
	"003-modify_aspect_ratio_with_respect_to_screen_resolution.patch"
)
sha256sums=(
	'SKIP'
	'a3a3798e21cd1da287d6abe971454384eccfeaf2af001301a7f585edaea83fa9'
	'0d2016d81c972ffa00002cda4f43163a112a4ec253d219e2d18840d26fe5ac8b'
	'8156db0fa448064abbcdb0ed9651347feff47bec0997e24a5aa791348443bc37'
	)
prepare() {
  cd xbmc-${_commit}

  find -type f -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i "{}" +
  sed 's|^#!.*python$|#!/usr/bin/python2|' -i tools/depends/native/rpl-native/rpl

  patch -p1 < ../001-add_mfc_decoding_toggle.patch
  patch -p1 < ../002-dont_try_to_decode_10bit_using_mfc.patch
  patch -p1 < ../003-modify_aspect_ratio_with_respect_to_screen_resolution.patch
}

build() {
  cd xbmc-${_commit}

  LIBCROSSGUID_ROOT="$(pwd)/tools/depends/target/crossguid"
  make -C tools/depends/target/crossguid "PREFIX=$LIBCROSSGUID_ROOT"

  CPPFLAGS+=" -I$LIBCROSSGUID_ROOT/include"
  LDFLAGS+=" -L/usr/lib/mali-egl -L$LIBCROSSGUID_ROOT/lib -I$LIBCROSSGUID_ROOT/include"

  # Bootstrapping
  MAKEFLAGS=-j1 ./bootstrap

  # Configuring XBMC
  export PYTHON_VERSION=2  # external python v2
  ./configure --prefix=$_prefix \
    gl_cv_func_gettimeofday_clobber=no ac_cv_lib_bluetooth_hci_devid=no \
    --disable-debug \
    --enable-optimizations \
    --enable-libbluray \
    --disable-texturepacker \
    --enable-external-libraries \
    --with-lirc-device=/run/lirc/lircd \
    --disable-vaapi \
    --disable-vdpau \
    --disable-gl \
    --disable-openmax \
    --disable-static --enable-shared \
    --disable-rsxs \
    --enable-gles \
    --enable-codec=mfc

  # Now (finally) build
  make
}

package_kodi-odroid() {
  pkgdesc="A software media player and entertainment hub for digital media (ODROID-X/X2/U2/U3)"

  # depends expected for kodi plugins:
  # 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  # depends expeced in FEH.py
  # 'mesa-demos' 'xorg-xdpyinfo'
  depends=(
    'python2-pillow' 'python2-pybluez' 'python2-simplejson'
    'mesa-demos' 'xorg-xdpyinfo'
    'bluez-libs' 'fribidi' 'glew' 'hicolor-icon-theme' 'libcdio'
    'libjpeg-turbo' 'libmariadbclient' 'libmicrohttpd' 'libpulse' 'libssh'
    'libva' 'libxrandr' 'libxslt' 'lzo' 'sdl2' 'smbclient' 'taglib' 'tinyxml'
    'yajl' 'odroid-libgl' 'mesa' 'libdcadec.so'
  )
  optdepends=(
    'afpfs-ng: Apple shares support'
    'bluez: Blutooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'lirc: Remote controller support'
    'libcec: CEC (Consumer Electronics Control) allows to control devices over HDMI port'
    'pulseaudio: PulseAudio support'
    'shairplay: AirPlay support'
    'udisks: Automount external drives'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level'
    'lsb-release: log distro information in crashlog'
  )
  install="kodi.install"
  provides=('xbmc' 'kodi')
  conflicts=('xbmc' 'kodi' 'shairplay-git')
  replaces=('xbmc')

  cd xbmc-${_commit}
  # Running make install
  make DESTDIR="$pkgdir" install

  # Licenses
  install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
  for licensef in LICENSE.GPL copying.txt; do
    mv ${pkgdir}${_prefix}/share/doc/kodi/${licensef} \
       ${pkgdir}${_prefix}/share/licenses/${pkgname}
  done
}

package_kodi-odroid-eventclients() {
  pkgdesc="Kodi Event Clients (ODROID-X/X2/U2/U3)"
  provides=('kodi-eventclients')
  conflicts=('kodi-eventclients')

  cd ${srcdir}/xbmc-${_commit}

  make DESTDIR="$pkgdir" eventclients WII_EXTRA_OPTS=-DCWIID_OLD

  install -dm755 "$pkgdir/usr/lib/python2.7/$pkgbase"
  mv "$pkgdir/kodi"/* "$pkgdir/usr/lib/python2.7/$pkgbase"
  rmdir "$pkgdir/kodi"
}
